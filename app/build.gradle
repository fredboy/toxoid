plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'androidx.navigation.safeargs.kotlin'
    id 'kotlinx-serialization'
    id 'kotlin-parcelize'
}

android {
    compileSdk setup.compileSdk

    defaultConfig {
        applicationId "ru.fredboy.toxoid"
        minSdk setup.minSdk
        targetSdk setup.targetSdk
        versionCode setup.versionCode
        versionName setup.versionName

        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled true
            applicationIdSuffix ".debug"
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
    buildFeatures {
        viewBinding true
    }
    packagingOptions {
        exclude "rootdoc.txt"
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ['build/jniLibs']
        }
    }
    kapt {
        generateStubs = true
    }
}

task buildMacros(type: Exec) {
    workingDir new File(project.projectDir.parentFile, 'jvm-macros')
    executable new File(project.projectDir.parentFile, 'sbt')
    args 'publishM2'
}

task buildSbtPlugins(type: Exec) {
    buildMacros
    workingDir new File(project.projectDir.parentFile, 'jvm-sbt-plugins')
    executable new File(project.projectDir.parentFile, 'sbt')
    args 'publishM2'
}

task buildToxCoreApi(type: Exec) {
    dependsOn buildSbtPlugins
    workingDir new File(project.projectDir.parentFile, 'jvm-toxcore-api')
    executable new File(project.projectDir.parentFile, 'sbt')
    args 'publishM2'
}

task buildToxCoreC(type: Exec) {
    dependsOn buildSbtPlugins, buildMacros, buildToxCoreApi
    workingDir new File(project.projectDir.parentFile, 'jvm-toxcore-c')
    executable 'make'
    args 'publishM2'
}

task publishLocalToxDependencies() {
    dependsOn buildMacros, buildSbtPlugins, buildToxCoreApi, buildToxCoreC
}

configurations {
    fixArchPath
}

tasks.register("fixPaths") {
    configurations.fixArchPath.asFileTree.forEach { jar ->
        String arch
        if (jar.name.contains("aarch64")) {
            arch = "arm64-v8a"
        } else if (jar.name.contains("arm")) {
            arch = "armeabi-v7a"
        } else if (jar.name.contains("i686")) {
            arch = "x86"
        } else if (jar.name.contains("x86_64")) {
            arch = "x86_64"
        } else {
            throw GradleException("Unknown arch")
        }

        new File("build/jniLibs/$arch").mkdirs()
        copy {
            from(zipTree(jar).files)
            into("build/jniLibs/$arch")
            include("*.so")
        }
    }
}

tasks.named("preBuild") {
    dependsOn fixPaths
}

dependencies {
    implementation "androidx.core:core-ktx:${versions.ktx}"
    implementation "androidx.appcompat:appcompat:${versions.appcompat}"
    implementation "com.google.android.material:material:${versions.material}"
    implementation "androidx.recyclerview:recyclerview:${versions.recyclerView}"
    implementation "androidx.annotation:annotation:${versions.annotation}"
    implementation "androidx.constraintlayout:constraintlayout:${versions.constraintLayout}"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:${versions.coroutines}"
    implementation "androidx.multidex:multidex:2.0.1"

//    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:${versions.serialization}"


    // navigation
    implementation "androidx.navigation:navigation-runtime-ktx:${versions.navigation}"
    implementation "androidx.navigation:navigation-fragment-ktx:${versions.navigation}"
    implementation "androidx.navigation:navigation-ui-ktx:${versions.navigation}"

    // materialdrawer
    implementation "com.mikepenz:materialdrawer:${versions.materialDrawer}"
    implementation "com.mikepenz:materialdrawer-nav:${versions.materialDrawer}"

    // fastadapter
    implementation "com.mikepenz:fastadapter:${versions.fastadapter}"
    implementation "com.mikepenz:fastadapter-extensions-binding:${versions.fastadapter}"
    implementation "com.mikepenz:fastadapter-extensions-diff:${versions.fastadapter}"

    // circleimage
    implementation "de.hdodenhof:circleimageview:${versions.circleimage}"

    // camerax
    implementation "androidx.camera:camera-camera2:${versions.camerax}"
    implementation "androidx.camera:camera-lifecycle:${versions.camerax}"
    implementation "androidx.camera:camera-view:${versions.camerax}"

    // zxing
    implementation "com.google.zxing:core:${versions.zxing}"

    // room
    implementation "androidx.room:room-runtime:${versions.room}"
    implementation "androidx.room:room-ktx:${versions.room}"
    kapt "androidx.room:room-compiler:${versions.room}"

    // moxy
    implementation "com.github.moxy-community:moxy-androidx:${versions.moxy}"
    implementation "com.github.moxy-community:moxy-material:${versions.moxy}"
    implementation "com.github.moxy-community:moxy:${versions.moxy}"
    implementation "com.github.moxy-community:moxy-ktx:${versions.moxy}"
    kapt "com.github.moxy-community:moxy-compiler:${versions.moxy}"

    // hilt
    implementation "com.google.dagger:hilt-android:${versions.hilt}"
    kapt "com.google.dagger:hilt-android-compiler:${versions.hilt}"

    // retrofit
    implementation "com.squareup.retrofit2:retrofit:${versions.retrofit}"
    implementation "com.squareup.retrofit2:converter-gson:${versions.retrofit}"

    // gson
    implementation "com.google.code.gson:gson:${versions.gson}"

    // splitties
    implementation "com.louiscad.splitties:splitties-fun-pack-android-base:${versions.splitties}"

    // rxjava
/*    implementation "io.reactivex.rxjava3:rxjava:${versions.rxjava}"
    implementation "io.reactivex.rxjava3:rxandroid:${versions.rxandroid}"*/

    // tox
    fixArchPath "org.toktok:tox4j-c_aarch64-linux-android:0.2.13"
    fixArchPath "org.toktok:tox4j-c_armv7a-linux-androideabi:0.2.13"
    fixArchPath "org.toktok:tox4j-c_x86_64-linux-android:0.2.13"
    fixArchPath "org.toktok:tox4j-c_i686-linux-android:0.2.13"
    implementation "org.toktok:tox4j-c_2.12:0.2.3"
    api "org.toktok:tox4j-api_2.12:0.2.3"

    // wtf
    implementation 'com.google.guava:listenablefuture:9999.0-empty-to-avoid-conflict-with-guava'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation "androidx.navigation:navigation-testing:${versions.navigation}"

}